% MetaFlop
% Standard 1.3

beginadjchar("A",5.5,cap#,0);	% box dimension definition


% sidebearings
numeric r,l;
% normal_adjust_fit(0.5space,0.5space);
normal_adjust_fit(space-0.3u#,space-0.3u#);

x3l=leftstemloc-cont*px/2;	% Position of the bottom left x cordinate, position of the leftstemloc 
x2r=w-x3l;			% Position of the bottom right x cordinate
(x4l+x1r)/2=.5w;		% Center of the tip 
x1r-x4l=corner;			% top corner
y1=y4=h;			% capital height
y2=y3=0;			% baseline

alpha:=diag_ratio(1,cont*px,h,x2r-x1r);	% diagonal definition from computer modern, see volume E, (to have optical correction turn 1 into new variable)

% penpositions of left and right stem
penpos1(alpha*cont*px,0);
penpos2(alpha*cont*px,0);
penpos3(alpha*px,0);
penpos4(alpha*px,0);

z0=whatever[z3r,z4r]=whatever[z1l,z2l];	% get inner corner point
y5=y6=bar*y0;				% relation between barheight and inner corner
z5=whatever[z3,z4]; z6=whatever[z1,z2];	% CORRECT TO THE PROPER ANGLE

% penposition of bar
penpos5(px,90);				% CORRECT TO THE PROPER ANGLE
penpos6(px,90);				% CORRECT TO THE PROPER ANGLE


fill z3l--z4l--z1r--z2r--z2l--z0--z3r--cycle;	% Here we use fill instead of penstroke
penstroke z5e..z6e;				% here we were lazy so we used penstroke

%penlabels(range 0 thru 30);			% labeling of pen positions including l and r, on greyfont outputs

endchar;
